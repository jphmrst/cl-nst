;;; File criteria.lisp
;;;
;;; This file is part of the NST unit/regression testing system.
;;;
;;; Copyright (c) 2006-2010 Smart Information Flow Technologies.
;;; Written by John Maraist.
;;; Derived from RRT, Copyright (c) 2005 Robert Goldman.
;;;
;;; NST is free software: you can redistribute it and/or modify it
;;; under the terms of the GNU Lesser General Public License as
;;; published by the Free Software Foundation, either version 3 of the
;;; License, or (at your option) any later version.
;;;
;;; NST is distributed in the hope that it will be useful, but WITHOUT
;;; ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
;;; or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Lesser General
;;; Public License for more details.
;;;
;;; You should have received a copy of the GNU Lesser General Public
;;; License along with NST.  If not, see
;;; <http://www.gnu.org/licenses/>.
(in-package :sift.nst)

;;; Documentation generator for criteria.

(defdoc:def-doctype criterion ())

;;; Built-in basic testing criteria.

(def-criterion (:pass (:forms) (:form chk))
  (declare (ignore chk))
  (make-success-report))
(defdoc:def-documentation (criterion :pass)
  (:intro (:latex "A trivial test, which always passes."))
  (:full (:seq
          (:plain "Example:")
          (:code "(def-test passing-test :pass 3 4 \"sd\")"))))

(def-criterion (:fail (:forms &rest args) (:form chk))
  (declare (ignore chk))
  (make-failure-report :format (car args) :args (cdr args)))
(defdoc:def-documentation (criterion :fail)
  (:callspec (format-string (:seq form)))
  (:intro (:latex "A trivial test, which always fails.  The format string and arguments should be suitable for the Lisp \\texttt{format} function."))
  (:full (:seq
          (:plain "Example:")
          (:code
           "(def-test fails (:fail \"Expected a \彳堍篝蜷铉堍潮博┅┅ㄤ彐泸轸弪轱ê麽蝾ê骘蝽蝈篝狎珞ê骘蝽汨氅ㄤ邈灬蝈ㄩ珙矧汨氅磲脲麽蝾轭绛蝈痫螋烘矧磲ㄣ狎狎珞横蜱ㄣ潋狎珞┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱瑚狎瞟ê汜祆箴邈ㄦ矧磲舡篝蜷铉ê箦骘蝽┅ê轭趄ê灬翦⑸篌蹂麽蝾轭绠澡骘蝽狒篝蜷铉犷狎珲礤铘箬秕熹忮篚轸徕戾骘翳涕箴苘翦趑酐骘蝽狒骢钽糸镱┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê麽蝾堍荥轶铒疱蜴邈篑踽蝈堍旦┅┅ㄤ彐泸轸弪轱ê趄蹂骘蝽ê骘蝽怙镬īㄩㄥ鲠怙镬磲脲篚沣弩蟓蝈痫螋磲脲驷殪躜瀛蝈痫螋烘矧磲⑴疱泗邃铒瞽铛祆顼艉螈横蜱扉篝怙镬┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱呼蝓瀛骘蝽ê汜祆箴邈ㄢ镲飑ê轭趄ê灬翦┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱ê趄蹂ê骘蝽螬ê鲠祯弩怙镬┅ㄩ怙镬磲脲篚沣弩蟓蝈痫螋磲脲驷殪躜瀛蝈痫螋烘矧磲⑴疱泗邃铒瞽铛祆顼艉螈横蜱扉篝怙镬┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱呼蝓濠ê汜祆箴邈īê轭趄⑴疱泗镱骘蝽麒殂轶弼犰踽翦狒翦篝轭糸礤翳泸轸弪轱蝈聃轵弩翳蝈篚祠麸忮铒瞽铋飚ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱ê羼ê骘蝽翎蜱弭ê鲠祯弩徙趱犰┅ㄩㄥㄥ鲠翎蜱弭徙趱犰磲脲篚沣弩蟓蝈痫螋磲脲驷殪躜瀛蝈痫螋烘矧磲⒅犰蹂铒羼麸鲠祯镦螈横蜱啜徙趱犰翎蜱弭┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱哄瘵ê汜祆箴邈翎蜱弭┅ê轭趄ê灬翦⒃桢泸轸弪轱狎珲礤铘犷翳骘蝽躅溴翦篝狎怙翳弼犰踽翦狒翦篝轭糸礤翳泸轸弪轱蝈聃轵弩翳狒翳蝈篚祠忮苘翦趑酐羼┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝羼ê羼рㄣ徜Ж悌┅┅┅ㄤ彐泸轸弪轱瞽犰獒ê簌礅镬钺礤啜哄К钺礤┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱后礅镬ê汜祆箴邈钺礤┅ê轭趄ê灬翦⒃桢骘蝽躅溴翦篝轶弼犰踽翦狒翦篝轭糸礤澡泸轸弪轱蝈聃轵弩翳狒翳蝈篚祠忮簌礅镬麒殂轶苘翦趑酐羼麸翳簌礅镬钺礤玳鲥狍翳泸轸弪轱狎珲礤铘┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝簌肀ê簌礅镬岍ㄣ狎Ж悌┅ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴ㄤ彐翦篝簌肀ê簌礅镬岍ㄣ徜Ж悌┅┅┅ㄤ彐泸轸弪轱ê羼ê骘蝽翎蜱弭ê鲠祯弩徙趱犰┅ㄩㄥ耢ㄥ鲠翎蜱弭徙趱犰磲脲篚沣弩蟓蝈痫螋磲脲驷殪躜瀛蝈痫螋烘矧磲⒅犰蹂铒羼麸鲠祯镦螈横蜱扉篝徙趱犰翎蜱弭┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱哄耢ê汜祆箴邈翎蜱弭┅ê轭趄ê灬翦⒃桢泸轸弪轱狎珲礤铘犷翳骘蝽躅溴翦篝狎怙翳弼犰踽翦狒翦篝轭糸礤翳泸轸弪轱蝈聃轵弩翳狒翳蝈篚祠忮苘翦趑酐羼忑┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝羼毂ê羼博ㄣ徜Ж畅┅ê泔溴┅┅ㄤ彐泸轸弪轱ê羼踽ê骘蝽翎蜱弭ê鲠祯弩徙趱犰┅ㄩㄥ聃犰ㄥ鲠翎蜱弭徙趱犰磲脲篚沣弩蟓蝈痫螋磲脲驷殪躜瀛蝈痫螋烘矧磲⒅犰蹂铒羼踽麸鲠祯镦螈横蜱扉篝徙趱犰翎蜱弭┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱哄聃犰ê汜祆箴邈翎蜱弭┅ê轭趄ê灬翦⒃桢泸轸弪轱狎珲礤铘犷翳骘蝽躅溴翦篝狎怙翳弼犰踽翦狒翦篝轭糸礤翳泸轸弪轱蝈聃轵弩翳狒翳蝈篚祠忮苘翦趑酐羼踽忑┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱ê羼踽祓ê骘蝽翎蜱弭ê鲠祯弩徙趱犰┅ㄩㄥ聃犰ㄥ鲠翎蜱弭徙趱犰磲脲篚沣弩蟓蝈痫螋磲脲驷殪躜瀛蝈痫螋烘矧磲⒅犰蹂铒羼踽祓麸鲠祯镦螈横蜱扉篝徙趱犰翎蜱弭┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱哄聃犰皓ê汜祆箴邈翎蜱弭┅ê轭趄ê灬翦⒃桢泸轸弪轱狎珲礤铘犷翳骘蝽躅溴翦篝狎怙翳弼犰踽翦狒翦篝轭糸礤翳泸轸弪轱蝈聃轵弩翳狒翳蝈篚祠忮苘翦趑酐羼踽祓┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱瞽犰獒ê骘蝽蟓羼啜吼蝈溟汜翦羼┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱烘矧眢羼ê汜祆箴邈īê轭趄ê灬翦⒃桢赭骘蝽躅溴翦篝狎怙翳弼犰踽翦狒翦篝轭糸礤翳泸轸弪轱蝈聃轵弩翳狒翳蝈篚祠忮苘翦趑酐羼┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝羼骘蝽蟊烘矧眢羼ㄣ徜Ж悌ㄣ徜潋Ж猢┅┅┅ㄤ彐泸轸弪轱瞽犰獒ê骘蝽蟓羼飑啜吼蝈溟汜翦羼飑ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱烘矧眢羼飑ê汜祆箴邈īê轭趄ê灬翦⒃桢赭骘蝽躅溴翦篝狎怙翳弼犰踽翦狒翦篝轭糸礤翳泸轸弪轱蝈聃轵弩翳狒翳蝈篚祠忮苘翦趑酐羼忑┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝羼戽矧眢烘矧眢羼ㄣ徜Ж悌ㄣ徜潋Ж畅┅┅┅ㄤ彐泸轸弪轱瞽犰獒ê骘蝽蟓羼踽飑啜吼蝈溟汜翦羼踽飑ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱烘矧眢羼踽飑ê汜祆箴邈īê轭趄ê灬翦⒃桢赭骘蝽躅溴翦篝狎怙翳弼犰踽翦狒翦篝轭糸礤翳泸轸弪轱蝈聃轵弩翳狒翳蝈篚祠忮苘翦趑酐羼踽忑┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱瞽犰獒ê鲠祯瀛扉篝骢螋桢颟啜横痧禊扉篝骢螋桢颟ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱忽犰蹂扉篝ê汜祆箴邈ㄦ躜翳弪┅ê轭趄ê灬翦⒚镱鲥螋眭祠轲戾鲠祯弩轭麸箝铉戾扉篝鲠祯瀹┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱ê痱邃殂狒ê骘蝽痱邃ê鲠祯弩蝈篝鲠祗┅ㄩㄡ痧禊ㄥ鲠啜骢钽糸镱痱邃┅鲠祗磲脲篚沣弩蟓蝈痫螋磲脲驷殪躜瀛蝈痫螋烘矧磲⑿蝈溟汜翦驷殪骘螈横蜱扉篝痱邃鲠祗┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱吼蝈溟汜翦ê汜祆箴邈痱邃┅ê轭趄ê灬翦⒃桢泸轸弪轱狎珲礤铘轶簌礅镬躅聃雉邃矧灬礅溽屮痱弩箝镱狒翦篝轭糸礤翳骘蝽躅溴翦篝狎弼犰踽翦犷疳篌邃麸翳溴铒翦骢钽糸镱澡泸轸弪轱屮疱泗翳狒翳蝈篚祠镦翳骢钽糸镱轶铒瞽铋飚┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝痱邃ê痱邃殂狒铛礅弪皓畅ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴ㄤ彐翦篝痱邃ê痱邃殂狒羼飑ǐ博畅┅┅ㄤ彐泸轸弪轱瞽犰獒ê潋镳鲠祯弩泸轸弪轱瞟啜横痧禊灬礅溽蝈篝雉桢蝮ㄤ邈灬蝈ㄩ珙矧徕戾雉桢蝮┅泸轸弪轱瞟ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱轰蝻瓠鲠祯弩ê汜祆箴邈ㄣ蜷翦蜷镱┅ê轭趄ê灬翦⒚桢汶翳痱轫狎鲠祯徙泔蜾轭麸翳篚怙蜾轭狒泸轸弪轱瞵殓铒蜷铉犷徜溟糸镱犰蝈趱蝾邃鲠祯弩骝镯翳弼犰踽糸镱镦翳骘蝽躅溴翦篝┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱ê漉眇骘蝽ê骘蝽忪躜猢ê鲠祯弩蝈篝骘蝽螬ㄦ矧磲狺簋" blurb forms)
  (make-failure-report :format "Arguments dumped" :args nil))
(defdoc:def-documentation (criterion :dump-forms)
  (:callspec (blurb))
  (:intro (:latex "For debugging NST criteria: fails after writing the current forms to standard output."))
  (:full (:seq
          (:plain "Example:")
          (:code "")
          (:plain "A example of a test which fails:")
          (:code ""))))

(def-criterion (:info (:forms string subcriterion) (:form expr-list-form))
  (let ((subcheck (check-criterion-on-form subcriterion expr-list-form)))
    (push string (check-result-info subcheck))
    subcheck))
(defdoc:def-documentation (criterion :info)
  (:callspec (string subcriterion))
  (:intro (:latex "Add an informational note to the check result."))
  (:full (:seq
          (:plain "Example:")
          (:code "(def-test known-bug (:info \"Known bug\" (:eql 3)) 4)"))))

(def-criterion (:err (:forms &key (type 'error)) (:form expr-form))
  (block err-criterion
    (format-at-verbosity 4 "    Setting up s handler for :err%" type)
    (handler-bind-interruptable
        ((condition #'(lambda (e)
                        (cond
                         ((typep e type)
                          (format-at-verbosity 4
                              "Caught s as expected by :err criterion%" e)
                          (return-from err-criterion (make-success-report)))
                         ((typep e 'error)
                          (format-at-verbosity 4
                              "Caught s but :err expected s%" e type)
                          (unless *debug-on-error*
                            (return-from err-criterion
                              (make-error-report e))))))))
      (eval expr-form))
    (make-failure-report :format "@<No expected error:簋壕横蜱啜ㄣ镱è犷扉篝屮痱骘蝽ㄥъ轶ㄣ狎屮痱骘蝽┅ㄣ潋屮痱骘蝽┅扉篝屮痱骘蝽┅┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱哄蝌ê汜祆箴邈é脲豉疱锰劣萤┅ê轭趄ê灬翦⒘翦篝轭糸礤弼犰踽翦翳骘蝽躅溴翦篝屮疱泗轭翳弼犰踽糸镱麸蜥轶箫礤泔钿轸轱町涉翳苘翦糸酐锰劣育狎珲礤铘轶篚痧扉邃翳泸轸弪轱屮疱泗翳蜥轶邃泔钿轸轱麸忮篚忏灬篌物翦翳狒翳钺礤镦翳豉疱箬秕熹苘屙痂铒酏忮聃雉邃轸轶铒弼犰踽翦洚┅ê骢祆ê箦ê痨衢⑴犴痨弩孩ê泔溴ㄤ彐翦篝弪虮ê弪呼疱弪蝻颟ㄥ蝌矧堍翳轶箬秕熹忮汜蹒梏堍┅ê泔溴ㄤ彐翦篝弪虿ê弪颟ㄥ蝌矧堍翳轶箬秕熹忮汜蹒梏堍┅┅┅ㄤ彐泸轸弪轱ê疱蜴ê骘蝽脲眢铋眢篚痧皓箦铋箦悱篚痧皓黹铋黹瞽篚痧皓ê骘蝽屮痱骘蝽┅ㄣ镱è矧箦悱篚痧黹瞽篚痧眢篚痧皓麒孱矧ㄡ钿箦悱篚痧黹瞽篚痧皓ㄡ钿眢篚痧黹瞽篚痧皓ㄡ钿眢篚痧箦悱篚痧皓ㄥ蝌矧⑼蹯糸痨糸礤礤趄殂玳鲥睥┅麒孱箦悱篚痧箦翩眢í卑鞍箦悌┅麒孱黹瞽篚痧箦翩眢í栋鞍黹瞟┅戾舄è篝狎舡糸礤ㄧ弭轭翦蝾犰蝈犰糸礤┅ㄥ鲠飙蝈篚祠ㄥ鲠屮痱骘蝽┅ㄥ钿糸礤ㄧ弭轭翦蝾犰蝈犰糸礤┅ㄥ灬痼邃眢íǒ卑鞍轭翦蝾犰糸礤躅轸蟓疱颦箦泔钿ō孱洵糸礤篝狎舡糸礤┅┅ㄤ邈灬蝈ㄩ珙矧弼犰蝈篚祠┅ㄩ眢屐狃箦洵眢磲脲篚沣弩蟓蝈痫螋磲脲驷殪躜瀛蝈痫螋烘矧磲⑴邈豸轱糸礤漤屮沐邃邃犰祜麇糸礤漤螈横蜱扉篝屐狃箦洵眢眢┅┅ㄥ蝌矧⒑疱蜴汨邈蝈聃轵弩疱蜴矧磲钽泸轸弪獒箴邈殒殂狒轱睥┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱吼弪姗ê汜祆箴邈é脲眢蜕烫捎琶萤箦优孟文萤黹蜕握耘萤┅ê轭趄ê灬翦⑴鲠祯狒弩翳骘蝽躅溴翦篝狒翦篝轭糸礤犷屮疱泗翳弼犰踽糸镱麸泔眇戾翦鏖翳轭翳玳鲥糸礤扉黹舢┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝疱蜴ê疱蜴喉轭博ㄡ汶旦┅┅ㄤ彐泸轸弪轱ê铒ê骘蝽篚忏蜷翦蜷镱ê骘蝽屮痱扉篝骘蝽┅戾è篚忏桢汶ㄣ桢汶泸轸弪轱瞽镱骘蝽篚忏蜷翦蜷镱屮痱扉篝骘蝽┅ㄣ镱è汨邈氕蝈篚祠弪蝻蝮篚忏桢汶篚忏桢汶è汨邈氕蝈篚祠驷殪躜弩篚忏桢汶磲脲篚沣弩蟓蝈痫螋洪铈ㄣ桢汶蝈篚祠轭骘篚忏桢汶┅磲脲驷殪躜瀛蝈痫螋烘矧磲⑴疱泗邃驷殪躜骝镯螈横蜱扉篝篚忏蜷翦蜷镱┅┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱侯雉ê汜祆箴邈篚忏蜷翦蜷镱┅ê轭趄ê灬翦⑿狍箦麒孱翦篝轭徙泔蜾轭麸苘翦趑酐篚忏蜷翦蜷镱驷殪ㄢ豸滹弩铒翳蝻犷弪蝻颟┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝铒舯ê铒ê簌礅镬猢п┅┅ㄤ彐泸轸弪轱ê犰ê骘蝽蝈篝篚忏蜷翦蜷岍ê骘蝽屮痱扉篝骘蝽┅戾è铙舡泔铘屮舡弼犰踽忪濯舂ㄤ邈灬蝈箴邈獒铙舡泔铘屮舡弼犰踽忪濯┅祜镳骘篚忏蜷翦蜷镱轭篚忏蜷翦蜷骘篚怛弩蹯ㄣ桢汶泸轸弪轱瞽镱骘蝽篚忏蜷翦蜷镱屮痱扉篝骘蝽狃疱钿ㄣ桢汶蝈篚祠麽蝾轭珞篚怛弩蹯舂轭麸麽蝾轭珞狃疱钿ㄣ桢汶蝈篚祠驷殪躜弩篚怛弩蹯舂轭麸驷殪躜弩狃疱钿ㄣ桢汶蝈篚祠弪蝻蝮篚怛弩蹯舂轭麸弪蝻蝮狃疱钿ㄣ桢汶蝈篚祠轭骘篚怛弩蹯舂轭麸轭骘骈钺祆蝈趱蝾磲脲犷洵汜扉怛狒瀛汨邈氕蝈篚祠瑚狎铋铉麽蝾轭珞烘衢祯蝈驷殪躜弩哄蝌矧弪蝻蝮洪铈轭骘┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱横祆ê汜祆箴邈é蝈篝ê箦泸轸弪轱瞟┅ê轭趄ê灬翦⒃栝泸轸弪轱怛轭珞箦鲥蜥雉桢泸轸弪獒躅溴镱汨邈氍犷鲥蜷骈弩翳狒翳妁犰疳篌┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐汨邈铒舯īê犰ê痱邃殂狒弼孱皓ê痱邃殂狒痱轫瀛皓博┅┅ㄤ彐泸轸弪轱ê犷ê骘蝽蝈篝泸轸弪獒ê骘蝽屮痱扉篝骘蝽┅戾è铙舡泔铘屮舡弼犰踽忪濯舂ㄩ铈铋飑ㄢ祜汶犷泸轸弪轱祜镳骘泸轸弪轱轭泸轸弪獒滹换ㄦ矧磲⒃蝙轭镱簋ア泸轸弪轱屮痱扉篝骘蝽戾舄è蝈篚祠ㄣ桢汶泸轸弪轱瞽镱骘蝽泸轸弪轱屮痱扉篝骘蝽┅蜴ㄣ桢汶蝈篚祠驷殪躜弩蝈篚祠┅蝈ㄣ桢汶蝈篚祠弪蝻蝮蝈篚祠┅蜷ㄣ桢汶蝈篚祠轭骘蝈篚祠┅换ㄦ矧磲蝈篚祠轶簋ア蝈篚祠换ㄦ矧磲蝈簋ア蝈换ㄦ矧磲蜴簋ア蜴换ㄦ矧磲蜷簋ア蜷麒孱蝈箦翩轭骘钽镱轭骘蝈┅麒孱蜴箦翩轭骘钽镱轭骘蜴┅麒孱蜷箦翩轭骘钽镱轭骘蜷┅躅戾篌矧蝈蜴蝈趱蝾骝镯犷泸轸弪轱磲脲篚沣弩蟓蝈痫螋洪铈钽镱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅┅┅磲脲驷殪躜瀛蝈痫螋烘矧磲⑽溟箨躅泗篚沣邋溴浜簋横蜱扉篝泸轸弪獒洪铈轭骘┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱横铢ê汜祆箴邈é蝈篝ê箦泸轸弪轱瞟┅ê轭趄ê灬翦⑿狍箦麒孱犷镦翳篚怙蜾轭狒泸轸弪獒疳篌┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐汨邈铒舯īê犷ê痱邃殂狒弼孱皓ê痱邃殂狒痱轫瀛皓旦┅┅ㄤ彐泸轸弪轱ê狃痨ê骘蝽趄犷箧矧泸轸弪轱瞟ê骘蝽屮痱蟓骘蝽┅ㄣ桢汶泸轸弪轱瞽镱骘蝽泸轸弪轱啜眭祠轲戾鲠祯瀛汜祆＇扉篝ㄡ痧禊＇趄犷箧矧屮痱蟓骘蝽┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱横痧禊ê汜祆箴邈ㄆ瘴迷上靡稍乓上惟ê轭趄ê灬翦⒘翦篝轭糸礤骈蝮弼犰踽翦翳骘蝽躅溴翦篝狃痨轭苘翦趑酐普蚊陨衔麸翳屙澡秭弪犰泸轸弪轱疳篌弩矧驷殪屮徙綮麒孱翳篚怙蜾轭狒苘翦趑酐靡稍乓上锡鏖翳翳狃痨殂狒轱瞌眭祠轲戾蝈篚祠鲠祯弩┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝狃痨汨邈ê狃痨汜潋ê羼卑┅Ж卑舶┅┅┅ㄤ彐泸轸弪轱ê汨邈氕弪ê骘蝽泸轸弪轱瞟ê骘蝽骘蝽螬戾è蝈篚祠ㄣ桢汶泸轸弪轱瞽镱骘蝽泸轸弪轱骘蝽螬┅ㄣ镱è汨邈氕蝈篚祠弪蝻蝮蝈篚祠磲脲篚沣弩蟓蝈痫螋┅磲脲驷殪躜瀛蝈痫螋烘矧磲兰物屮疱泗邃弪蝻骘汨邈镱湖簋壕横蜱扉篝泸轸弪轱ㄣ镱è犷扉篝骘蝽螬ㄥъ轶ㄣ狎骘蝽螬┅ㄣ潋骘蝽螬扉篝骘蝽螬┅┅┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱恒桢汶弪颟ê汜祆箴邈ㄣ蜷翦蜷镱┅ê轭趄ê灬翦⑻殡苘翦趑酐哄蝌怩痱镢邋潴徙泔蜾轭麸翳篚怙蜾轭狒泸轸弪轱蜥翳弪翳犷箝眇禊弼犰踽糸铉翳轭瘐骘蝽螽┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝汨邈氕弪虮ê汨邈氕弪烘矧眢羼п箐骁ㄥ蝌矧堍翳轶箬秕熹忮汜蹒梏堍┅┅┅ㄤ彐泸轸弪轱ê痱镧ê骘蝽蝈篝骘蝽蟓犷洵泸轸弪轱瞟ê骘蝽骘蝽螬戾è痱镧瞽骘蝽ㄢ豸灬篝骘蝽蟓犷洵泸轸弪轱瞟ㄣ蜷翦蜷镱ㄣ狎灬篝骘蝽蟓犷洵泸轸弪轱瞟┅祜镳骘骘蝽轭痱镧瞽骘蝽滹ㄥ鲠骘蝽┅ㄣ桢汶泸轸弪轱瞽镱骘蝽泸轸弪轱骘蝽螬┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱吼蝻珙ê汜祆箴邈é蝈篝骘蝽蟓犷洵泸轸弪轱瞟ê轭趄ê灬翦⒘翦篝轭糸礤骈蝮弼犰踽翦翳苘翦趑酐葡彝轭矧溴颥犷翳孱痱镢邋潴鏖翳弼犰踽糸镱镦翳骘蝽躅溴翦篝徙泔蜾轭麸翳篚怙蜾轭狒泸轸弪轱町┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝骘蝽ê痱镧箦翩畅ê羼畅┅┅ㄤ彐泸轸弪轱ê痱镪ê骘蝽轭溟沐泸轸弪轱瞟ê鲠祯弩蝈篝鲠祯弩┅ㄣ桢汶泸轸弪轱瞽镱骘蝽泸轸弪轱啜扉篝括祜镳骘殇轭轭溟沐泔祆邈唰铘殇鲠祯弩┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱吼蝻戛ê汜祆箴邈ㄩ钿殂弩泸轸弪轱瞟ê轭趄ê灬翦⒁遽蝌犷珏翳骘蝽躅溴翦篝怡箦戾泗轭铄扉篝徙泔蜾轭麸翳轭溴铛礅弪轭麸翳镬扉篝描邈腴铉镦翳蝈矧玑铋邃骘蝽泔铘轭蹂徙泔蜾轭麸翳篚怙蜾轭狒泸轸弪轱町┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝痱镪ê痱镪ò博烘矧眢羼пㄣ狎Ж猢┅┅┅ㄤ彐泸轸弪轱ê鲠祯弩ê骘蝽蝈篝狎珞ê骘蝽骘蝽┅ㄣ桢汶泸轸弪轱瞽镱骘蝽啜后羼泪蜱螬啜扉篝骘蝽┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱忽犰蹂螬ê汜祆箴邈é蝈篝狎珞┅ê轭趄ê灬翦⒚桢汶遽汨镦翳骘蝽躅溴翦篝徙泔蜾轭麸翳蝈箴邈糸鲥篚怙蜾轭狒泸轸弪轱町┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱ê遽汨ê骘蝽泸轸弪轱瞟ê鲠祯弩飑ㄢ祜汶遽汨戾è轭骘铋飑麽蝾轭珞铋飑祜镳骘鲠祯轭滹戾è蝈篚祠ㄣ桢汶泸轸弪轱瞽镱骘蝽泸轸弪轱啜扉篝К鲠祯濠┅ㄣ镱è矧ㄣ桢汶蝈篚祠弪蝻蝮蝈篚祠ㄣ桢汶蝈篚祠驷殪躜弩蝈篚祠┅箦翩ㄣ桢汶蝈篚祠轭骘蝈篚祠ㄡ痧孱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠ㄡ痧孱麽蝾轭珞ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠┅蝈趱蝾骝镯遽汨蝈篚祠┅箦翩轭骘ㄡ痧孱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅麽蝾轭珞ㄡ痧孱麽蝾轭珞ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠┅┅┅磲脲篚沣弩蟓蝈痫螋洪铈轭骘瑚狎铋铉麽蝾轭珞┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱哄徙瑭ê汜祆箴邈ㄣ蜷翦蜷镱┅ê轭趄ê灬翦⒘翦篝轭糸礤弼犰踽翦翳骘蝽躅溴翦篝屮疱泗轭麸骈钿扉篝狍蝈篚祠砒疱泗翳狒遽汨狎珲礤铘镦翳扉篝徙泔蜾轭麸翳篚怙蜾轭狒苘翦趑酐泸轸弪轱铨犷疳篌弩麒孱犰镦翳弩汨邈塍疳篌┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐翦篝遽汨ê遽汨ê簌礅镬岍Ж岍┅┅ㄤ彐泸轸弪轱ê箦ê骘蝽蝈篝泸轸弪獒ê鲠祯弩飑ㄢ祜汶箦戾è轭骘铋飑麽蝾轭珞铋飑躅戾篌ㄥ耢戾铉翳飑戾铉翳泸轸弪獒┅蝈趱蝾骝镯箦磲脲驷殪躜瀛蝈痫螋烘矧磲⑴疱泗邃扉篝镦戾铉翳洮顼螈横蜱啜戾铉翳泸轸弪獒飑┅祜镳骘泸轸弪轱轭泸轸弪獒骘轸屙轭滹戾è蝈篚祠ㄣ桢汶泸轸弪轱瞽镱鲠祯泸轸弪轱轸屙┅ㄣ镱è矧ㄣ桢汶蝈篚祠弪蝻蝮蝈篚祠ㄣ桢汶蝈篚祠驷殪躜弩蝈篚祠┅箦翩ㄣ桢汶蝈篚祠轭骘蝈篚祠ㄡ痧孱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠ㄡ痧孱麽蝾轭珞ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠┅蝈趱蝾骝镯箦蝈篚祠┅箦翩轭骘ㄡ痧孱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅麽蝾轭珞ㄡ痧孱麽蝾轭珞ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠┅┅┅磲脲篚沣弩蟓蝈痫螋洪铈轭骘瑚狎铋铉麽蝾轭珞┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱后羼ê汜祆箴邈é蝈篝泸轸弪獒┅ê轭趄ê灬翦⑴鲠祯狒弩轸轭瘐骘蝽汨邈塍遽汨镦轸屐屙孱趔徙泔蜾轭麸翳蝈箴邈糸鲥篚怙蜾轭狒泸轸弪轱瞵犷疳篌弩麒孱犰镦翳屙疳篌┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐汨邈箦胥桢汶ê箦ê痱邃殂狒簌礅镬皓ê羼暴ê簌礅镬洎Ж洎┅┅ㄤ彐泸轸弪轱ê疱蝽豸ê骘蝽泸轸弪轱瞟ê鲠祯弩飑ㄢ祜汶疱蝽豸瀛忪镢戾è疱蝽磲脲轭篝犷沐ю弪眭翦后蜚飑┅祜镳麒殪ㄨ狍铄疱蝽螬滹戾舄è铄舡疱蝽豸狒轱疱蝽螬蝈篚祠ㄣ桢汶泸轸弪轱瞽镱鲠祯泸轸弪轱┅麒孱ㄡ钿铛祆ㄣ桢汶蝈篚祠弪蝻蝮蝈篚祠┅铛祆ㄣ桢汶蝈篚祠驷殪躜弩蝈篚祠┅蝈趱蝾骝镯疱蝽豸瀛忪镢磲脲篚沣弩蟓蝈痫螋┅┅磲脲驷殪躜瀛蝈痫螋烘矧磲⑽疱蝽豸狒轱镦筢糸箧殄螈横蜱啜泸轸弪轱瞟┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱吼弪眭翦ê汜祆箴邈ㄣ蜷翦蜷镱┅ê轭趄ê灬翦⒘翦篝轭糸礤弼犰踽翦翳骘蝽躅溴翦篝屮疱泗轭麸骈钿扉篝狍蝈篚祠澡泸轸弪轱屮疱泗麸骈钿翳狒箫礤疱蝽豸狒轱镦翳轶扉篝鏖祆筢糸箧翳篚怙蜾轭狒泸轸弪轱町┅ê骢祆ê箦ê痨衢⑴犴痨弩孩ê泔溴ㄤ彐翦篝疱蝽豸灞ê疱蝽豸ê遽汨ê羼п┅Ж岍ê泔溴ㄤ彐汨邈疱蝽豸宀ê疱蝽豸ê箦ê簌礅镬猢ê痱邃殂狒簌礅镬皓ê痱邃殂狒铛礅弪皓┅Ж猢┅┅ㄤ彐泸轸弪轱ê徙蝻篌ê骘蝽蝈篝泸轸弪獒ê鲠祯弩雯ㄢ祜汶徙蝻篌忪镢戾è轭骘铋飑麽蝾轭珞铋飑躅戾篌ㄥ耢戾铉翳雯戾铉翳泸轸弪獒┅蝈趱蝾骝镯徙蝻篌忪镢磲脲驷殪躜瀛蝈痫螋烘矧磲⑴疱泗邃箦聃孱沐镦戾铉翳洧横蜱啜戾铉翳泸轸弪獒┅┅祜镳骘泸轸弪轱轭泸轸弪獒骘殇骝镯滹戾è蝈篚祠ㄣ桢汶泸轸弪轱瞽镱鲠祯泸轸弪轱ㄥ祠殇┅┅ㄣ镱è矧ㄣ桢汶蝈篚祠弪蝻蝮蝈篚祠ㄣ桢汶蝈篚祠驷殪躜弩蝈篚祠┅箦翩ㄣ桢汶蝈篚祠轭骘蝈篚祠ㄡ痧孱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠ㄡ痧孱麽蝾轭珞ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠┅蝈趱蝾骝镯徙蝻篌忪镢蝈篚祠┅箦翩轭骘ㄡ痧孱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅麽蝾轭珞ㄡ痧孱麽蝾轭珞ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠┅┅┅磲脲篚沣弩蟓蝈痫螋洪铈轭骘瑚狎铋铉麽蝾轭珞┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱横泸矬螬ê汜祆箴邈é蝈篝泸轸弪獒┅ê轭趄ê灬翦⑻殡苘翦趑酐后羼怩骘鲥泗矧轭篝遽镦扉篝┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐汨邈徙蝻篌ê徙蝻篌ê痱邃殂狒簌礅镬皓ê羼暴鲥泗矧п暴┅┅ㄤ彐泸轸弪轱ê箪雉ê骘蝽蝈篝沆狨箦螬ê鲠祯弩镡戛ㄢ祜汶箪雉蟓忪镢戾è麽蝾轭珞铋飑ㄩ铈铋飑祜镳骘箪雉泸轸弪轱瞟轭沆狨箦滹戾è鲠祯箪雉鲠祯镡箪雉┅戾è蝈篚祠ㄣ桢汶泸轸弪轱瞽镱鲠祯泸轸弪轱鲠祯濠┅ㄣ镱è矧ㄣ桢汶蝈篚祠弪蝻蝮蝈篚祠ㄣ桢汶蝈篚祠驷殪躜弩蝈篚祠┅箦翩ㄣ桢汶蝈篚祠轭骘蝈篚祠ㄡ痧孱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠ㄡ痧孱麽蝾轭珞ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠┅蝈趱蝾骝镯箪雉蟓忪镢蝈篚祠┅箦翩轭骘ㄡ痧孱轭骘ㄣ桢汶蝈篚祠轭骘蝈篚祠┅麽蝾轭珞ㄡ痧孱麽蝾轭珞ㄣ桢汶蝈篚祠麽蝾轭珞蝈篚祠┅┅┅┅磲脲篚沣弩蟓蝈痫螋洪铈轭骘瑚狎铋铉麽蝾轭珞┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱后祜趔ê汜祆箴邈é蝈篝沆狨箦螬ê轭趄ê灬翦⑴鲠祯狒弩轸轭瘐骘蝽犷疳篌弩麒孱翳鲠祯狒遽汨玳鲥箪雉筢糸箧殄翳泔蝌弩痫钿轭篚怙蜾轭狒泔铙趄衢铘┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ㄤ彐沆狍沆狍筱桢汶īè蟊洪铋翎蜱后候遽溴珏舡蟊蟛洪铋翎蜱后博蟪洪铋翎蜱后畅┅ㄤ彐翦篝箪雉ê箪雉蟊ê羼卑┅蟛ê簌礅镬┅蟪ê箦ê簌礅镬瘵ê簌礅镬鳗ê簌礅镬濠ê簌礅镬颟┅磲脲轭篝犷沐с灬篌汨邈后卑后后Ж颟┅┅┅ㄤ彐躅蝈骈铄疳汶徵瀛簌礅镬溴箝珞疳汶徵瀛溴箝簌礅镬溴箝绌ㄢ祜汶蝈骈铄戾è疳汶徵ㄣ镱è疳汶徵屦疳汶徵瀛溴箝绌疳汶徵瀛溴箝绌è簌礅镬疳汶徵瀛溴箝绌ㄦ轭洵疳汶徵簌礅镬钺礤疳汶徵瀛溴箝绌┅è篝蜷铉疳汶徵瀛溴箝绌ㄦ轭洵疳汶徵疳汶徵瀛溴箝绌蝈趱蝾骝镯蝈骈铄鲠祯弩磲脲驷殪躜瀛蝈痫螋烘矧磲滹弩铒溴箝珙狒疳汶徵澧横蜱扉篝疳汶徵瀛溴箝绌铋铋飑┅┅簌礅镬篝蜷铉ㄣ镱è簌礅镬簌礅镬溴箝绌簌礅镬钺礤簌礅镬溴箝绌è篝蜷铉簌礅镬溴箝绌簌礅镬溴箝绌蝈趱蝾骝镯蝈骈铄鲠祯弩磲脲驷殪躜瀛蝈痫螋烘矧磲滹弩铒溴箝珙狒簌礅镬横蜱扉篝簌礅镬溴箝绌铋铋飑┅┅鲠祯弩铋疳汶徵簌礅镬篝蜷铉┅┅ㄤ彐泸轸弪轱ê疳汶徵瀛屮痫螋ê骘蝽疳汶徵瀛溴箝绌ê鲠祯弩簌礅镬溴箝绌ㄢ祜汶泸轸眭祠轲戾鲠祯瀛忾钿ㄦ衢疳汶徵簌礅镬篝蜷铉蝈骈铄疳汶徵瀛簌礅镬溴箝珞疳汶徵瀛溴箝簌礅镬溴箝绌麒孱驷殪蝈趱蝾骝镯泸轸驷殪┅眭祠轲戾鲠祯瀛忾钿ㄡ泗踽篝狒躞ㄦ轭洵簌礅镬簌礅镬篝蜷铉疳汶徵濠ㄤ邈灬蝈ㄩ珙矧徙趱犰┅ㄣ狍篝狒躞è铋飑磲脲驷殪躜瀛蝈痫螋烘矧磲⑽簌礅镬轭疳汶徵幄横蜱扉篝簌礅镬篝蜷铉疳汶徵瀛钺礤疳汶徵濠┅è洪铊弪轸邃磲脲驷殪躜瀛蝈痫螋烘矧磲⒂礅镬轶轭桢蜷翦怩铒屮痫螋邃怡幄横蜱扉篝簌礅镬篝蜷铉疳汶徵瀛钺礤疳汶徵濠┅è哄翦蝾犰磲脲篚沣弩蟓蝈痫螋┅è洪铘弪钺飑磲脲驷殪躜瀛蝈痫螋烘矧磲⒂礅镬轶轭翦蝾犰铒屮痫螋邃轭幄横蜱扉篝簌礅镬篝蜷铉疳汶徵瀛钺礤疳汶徵濠┅雉桢蝼轶磲脲弪蝻颦蝈痫螋烘矧磲⒄铄疱泗邃篝狒躞蝈篚祠骝镯骈钿簌礅镬螈横蜱扉篝篝狒躞┅┅┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱吼徙脶珏屮痫螋螬ê汜祆箴邈疳汶徵瀛溴箝绌ê轭趄ê灬翦┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅ㄤ彐泸轸弪轱ê疳汶徵瀛轭翦蝾犰ê骘蝽疳汶徵瀛溴箝绌ê鲠祯弩簌礅镬溴箝绌ㄢ祜汶泸轸眭祠轲戾鲠祯瀛忾钿ㄦ衢疳汶徵簌礅镬篝蜷铉蝈骈铄疳汶徵瀛簌礅镬溴箝珞疳汶徵瀛溴箝簌礅镬溴箝绌麒孱驷殪蝈趱蝾骝镯泸轸驷殪┅眭祠轲戾鲠祯瀛忾钿ㄡ泗踽篝狒躞ㄦ轭洵簌礅镬簌礅镬篝蜷铉疳汶徵濠ㄤ邈灬蝈ㄩ珙矧徙趱犰┅ㄣ狍篝狒躞è铋飑磲脲驷殪躜瀛蝈痫螋烘矧磲⑽簌礅镬轭疳汶徵幄横蜱扉篝簌礅镬篝蜷铉疳汶徵瀛钺礤疳汶徵濠┅è洪铊弪轸邃磲脲驷殪躜瀛蝈痫螋烘矧磲⒂礅镬轶轭桢蜷翦洮怩铒屮痫螋邃怡幄横蜱扉篝簌礅镬篝蜷铉疳汶徵瀛钺礤疳汶徵濠┅è哄翦蝾犰磲脲驷殪躜瀛蝈痫螋烘矧磲⒂礅镬轶屮痫螋邃铒轭翦蝾犰轭幄横蜱扉篝簌礅镬篝蜷铉疳汶徵瀛钺礤疳汶徵濠┅è洪铘弪钺飑磲脲篚沣弩蟓蝈痫螋┅雉桢蝼轶磲脲弪蝻颦蝈痫螋烘矧磲⒄铄疱泗邃蝈篚祠骝镯骈钿簌礅镬┅┅┅ㄤ彐滹愫溴姝滹沲礤铘狒轱ㄣ蜷翦蜷镱吼徙脶珏轭翦蝾犰ê汜祆箴邈疳汶徵瀛溴箝绌ê轭趄ê灬翦┅ê骢祆ê箦ê痨衢⑴犴痨搴ê泔溴ê痨衢⒘屮犴痨镦翦篝麒殂驷殪蠛ê泔溴┅┅